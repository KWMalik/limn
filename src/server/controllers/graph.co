fs         = require 'fs'
path       = require 'path'
{existsSync:exists} = path

_          = require 'underscore'
Seq        = require 'seq'
yaml       = require 'js-yaml'
{mkdirp, mkdirpAsync} = require '../mkdirp'

{ readJSONFilesAsync,
} = require '../files'

Controller = require '../controller'


/**
 * @class Resource controller for graph requests.
 */
class GraphController extends Controller
    PROTECTED_GRAPH_IDS : <[
        unique_visitors
        pageviews
        pageviews_mobile
        reach
        commons
        articles
        articles_per_day
        edits
        new_editors
        active_editors
        active_editors_target
        very_active_editors
    ]>
    PROTECT_GRAPHS : true
    
    name    : 'graphs'
    dataDir : null
    
    ->
        super ...
        limnOpts = @app.set 'limn options'
        @dataDir = "#{limnOpts.dataDir}/graphs"
    
    
    
    toFile: (id) -> "#{@dataDir}/#id.json"
    
    /**
     * Auto-load :id for related requests.
     */
    autoload: (id, cb) ->
        file = @toFile id
        parser = JSON.parse
        
        yamlFile = file.replace /\.json$/i, '.yaml'
        if exists yamlFile 
            file = yamlFile
            parser = yaml.load
        
        err, data <- fs.readFile file, 'utf8'
        
        if 'ENOENT' is err?.code
            return cb null, {}
        if err
            console.error "GraphController.autoload(#id, #{typeof cb}) -->\nerr"
            return cb err
        try
            cb null, parser data
        catch err
            console.error "GraphController.autoload(#id, #{typeof cb}) -->\nerr"
            cb err
    
    # GET /graphs.:format?
    index: (req, res) ->
        # if format is json, then return the graph JSON
        switch req.format
            case 'json' 
                pattern = "#{@dataDir}/*.json"
                Seq()
                    # find list of data/graph/*.json files, send back the objects
                    .seq            -> readJSONFilesAsync pattern, this
                    # remove filepath info
                    .seq   (graphs) -> res.send _.values graphs
            default 
                res.render 'graph/index'
    
    # GET /graphs/:graph
    show: (req, res) ->
        if req.format is 'json'
            res.send req.graph
        else
            res.render 'graph/view'
    
    # GET /graphs/:graph/edit
    edit: (req, res) ->
        if req.format is 'json'
            res.send req.graph
        else
            res.render 'graph/edit'
    
    # GET /graphs/new
    new: (req, res) ->
        res.render 'graph/edit'
    
    # POST /graphs
    create: (req, res) ->
        return unless data = @processBody req, res
        file = @toFile data.id
        if exists file
            return res.send { result:"error", message:"Graph '#{data.id}' already exists!" }, 409 # 409: Resource Conflict
        else
            fs.writeFile file, JSON.stringify(data), "utf8", @errorHandler(res, "Error writing graph!")
    
    # PUT /graphs/:graph
    update: (req, res) ->
        return unless data = @processBody req, res
        if @PROTECT_GRAPHS and _ @PROTECTED_GRAPH_IDS .contains data.id
            return res.send {result:"error", message:"Graph '#{data.id}' is read-only."}, 403 # 403: Forbidden
        fs.writeFile @toFile(data.id), JSON.stringify(data), "utf8", @errorHandler(res, "Error writing graph!")
    
    # DELETE /graphs/:graph
    destroy: (req, res) ->
        id = req.param.graph
        if @PROTECT_GRAPHS and _ @PROTECTED_GRAPH_IDS .contains id
            return res.send {result:"error", message:"Graph '#{id}' is read-only."}, 403 # 403: Forbidden
        fs.unlink @toFile(id), @errorHandler(res, "Graph '#{id}' does not exist!", 410) # 410: Resource Gone
    
    
    ### Helpers
    
    processBody: (req, res) ->
        unless req.body
            res.send {result:"error", message:"Data required!"}, 400 # 400: Bad Request
            return false
        
        data = req.body
        data.slug or= data.id
        data.id   or= data.slug
        
        unless data.slug
            res.send {result:"error", message:"Slug required!"}, 400 # 400: Bad Request
            return false
        
        mkdirp @dataDir unless exists @dataDir
        return data
    


module.exports = exports = GraphController
