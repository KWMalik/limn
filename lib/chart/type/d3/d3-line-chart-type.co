d3 = require 'd3'
ColorBrewer = require 'colorbrewer'

{ _, op,
} = require 'kraken/util'
{ ChartType,
} = require 'kraken/chart/chart-type'


class exports.LineChartType extends ChartType
    __bind__ : <[ determineSize ]>
    SPEC_URL : '/schema/d3/d3-line.json'
    
    # NOTE: ChartType.register() must come AFTER `typeName` declaration.
    typeName : 'd3-line'
    ChartType.register this
    
    
    /**
     * Hash of role-names to the selector which, when applied to the view,
     * returns the correct element.
     * @type Object
     */
    roles :
        viewport : '.viewport'
        legend   : '.graph-legend'
    
    
    
    -> super ...
    
    
    
    getData: ->
        @model.dataset.getColumns()
    
    
    transform: ->
        dataset = @model.dataset
        options = @model.getOptions() import @determineSize()
        options import do
            colors             : dataset.getColors()
        options
    
    
    renderChart: (data, viewport, options, lastChart) ->
        ### Starting with http://bost.ocks.org/mike/chart/
        
        margin = {top: 20, right: 20, bottom: 20, left: 20}
        width  = 760
        height = 320
        xScale = d3.time.scale()
        yScale = d3.scale.linear()
        
        dates  = data[0]
        cols   = data.slice(1)
        
        # Calculate extents using all the data points (but not dates)
        # allValues = d3.merge @model.dataset.getDataColumns()
        allValues = d3.merge cols
        
        
        # Update the x-scale with the extents of the dates.
        xScale
            .domain d3.extent dates
            .range [ 0, width - margin.left - margin.right ]
        
        # Update the y-scale with the extents of the data.
        yScale
            .domain d3.extent allValues
            .range [ height - margin.top - margin.bottom, 0 ]
        
        # Select the svg element, if it exists.
        svg = d3.select viewport.0 .selectAll "svg"
            .data [cols]
        
        # ...Otherwise, create the skeletal chart.
        enterFrame = svg.enter()
            .append "svg" .append "g"
                .attr "class", "frame"
        enterFrame.append "g"
            .attr "class", "x axis time"
        
        # Update chart dimensions.
        svg .attr "width", width
            .attr "height", height
        frame = svg.select "g.frame"
            .attr "transform", "translate(#{margin.left},#{margin.top})"
        
        # Update the x-axis.
        xAxis = d3.svg.axis().scale(xScale).orient("bottom").tickSize(6, 0)
        frame.select ".x.axis.time"
            .attr "transform", "translate(0,#{yScale.range()[0]})"
            .call xAxis
        
        # Create/Update the line paths.
        lines = frame.selectAll "path.line"
            .data cols.map -> d3.zip dates, it
        lines.enter().append "path"
            .attr "class", "line"
        lines.exit().remove()
        
        X = (d, i) -> xScale d[0]
        Y = (d, i) -> yScale d[1]
        line = d3.svg.line().x(X).y(Y)
        # frame.selectAll "path.line"
        lines.attr "d", line
            .style "stroke", (col, i) -> options.colors[i]
        
        svg
    
    
    
