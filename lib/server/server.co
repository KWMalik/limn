#!/usr/bin/env coco

fs       = require 'fs'
path     = require 'path'
{exec, spawn} = require 'child_process'

_        = require 'underscore'
express  = require 'express'
compiler = require 'connect-compiler-extras'


stylus   = require 'stylus'
nib      = require 'nib'



### Config

PORT = 8081

CWD = process.cwd()
WWW    = "#CWD/www"
VAR    = "#CWD/var"
STATIC = "#CWD/static"

NODE_ENV = process.env.NODE_ENV or 'development'


VERSION = 'dev'
err, stdout, stderr <- exec 'git rev-parse --short HEAD', {cwd:CWD, env:process.env}
throw err if err
VERSION = stdout.trim!

# err <- fs.writeFile 'lib/version.js', "module.exports = exports = '#{VERSION}';\n", 'utf8'
# try VERSION = require '../version' catch e


### Setup

exports.app = app = express.createServer()
app.listen PORT

console.log "starting Kraken dev server (port=#PORT, env=#NODE_ENV, version=#VERSION)"
console.log "========================================================================"

app.configure ->
    app.set 'views', WWW
    app.set 'view engine', 'jade'
    app.set 'view options', {
        layout  : false
        pretty  : true
        version : VERSION
        WWW     : WWW
        VAR     : VAR
        STATIC  : STATIC
    } import require './view-helpers'
    
    # app.use express.logger()
    app.use express.bodyParser()
    app.use express.methodOverride()
    
    log_level = 'INFO'
    
    app.use compiler do
        src     : WWW
        dest    : VAR
        enabled : <[ stylus coco pyyaml ]>
        options : stylus : { nib:true, include:"#WWW/css" }
        log_level : log_level
    
    # wrap modules in commonjs closure for browser
    app.use compiler do
        enabled : 'commonjs'
        src     : [ VAR, WWW, STATIC ]
        dest    : VAR
        options : commonjs : drop_path_parts:1
        log_level : log_level
    
    app.use require('browserify') do
        mount   : '/vendor/browserify.js'
        require : <[ events seq ]>
    
    app.use express.static WWW
    app.use express.static VAR
    app.use express.static STATIC
    app.use app.router
    app.use express.errorHandler do
        dumpExceptions : true
        showStack      : true


### Routes

app.get '/', (req, res) ->
    res.render 'graph'

app.get '/:page', (req, res) ->
    res.render req.params.page


