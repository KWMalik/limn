fs         = require 'fs'
path       = require 'path'
{existsSync:exists} = path

_          = require 'underscore'
yaml       = require 'js-yaml'
{mkdirp, mkdirpAsync} = require '../mkdirp'

Controller = require '../controller'


/**
 * @class Resource controller for graph requests.
 */
class GraphController extends Controller
    PROTECTED_GRAPH_IDS : <[
        unique_visitors
        pageviews
        pageviews_mobile
        reach
        commons
        articles
        articles_per_day
        edits
        new_editors
        active_editors
        active_editors_target
        very_active_editors
    ]>
    PROTECT_GRAPHS : true
    
    name    : 'graphs'
    dataDir : 'data/graphs'
    -> super ...
    
    
    toFile: (id) -> "#{@dataDir}/#id.json"
    
    /**
     * Auto-load :id for related requests.
     */
    autoload: (id, cb) ->
        file = @toFile id
        parser = JSON.parse
        
        yamlFile = file.replace /\.json$/i, '.yaml'
        if exists yamlFile 
            file = yamlFile
            parser = yaml.load
        
        err, data <- fs.readFile file, 'utf8'
        if 'ENOENT' is err?.code
            return cb null, {}
        if err
            console.error "GraphController.autoload(#id, #{typeof cb}) -->\nerr"
            return cb err
        try
            cb null, parser data
        catch err
            console.error "GraphController.autoload(#id, #{typeof cb}) -->\nerr"
            cb err
    
    # GET /graphs
    index: (req, res) ->
        res.render 'graph/index'
    
    # GET /graphs/:graph
    show: (req, res) ->
        if req.format is 'json'
            res.send req.graph
        else
            res.render 'graph/view'
    
    # GET /graphs/:graph/edit
    edit: (req, res) ->
        if req.format is 'json'
            res.send req.graph
        else
            res.render 'graph/edit'
    
    # GET /graphs/new
    new: (req, res) ->
        res.render 'graph/edit'
    
    # POST /graphs
    create: (req, res) ->
        return unless data = @processBody req, res
        file = @toFile data.id
        if exists file
            return res.send { result:"error", message:"Graph '#{data.id}' already exists!" }
        else
            fs.writeFile file, JSON.stringify(data), "utf8", @errorHandler(res, "Error writing graph!")
    
    # PUT /graphs/:graph
    update: (req, res) ->
        return unless data = @processBody req, res
        if @PROTECT_GRAPHS and _ @PROTECTED_GRAPH_IDS .contains data.id
            return res.send {result:"error", message:"Graph '#{data.id}' is read-only."}, 403
        fs.writeFile @toFile(data.id), JSON.stringify(data), "utf8", @errorHandler(res, "Error writing graph!")
    
    # DELETE /graphs/:graph
    destroy: (req, res) ->
        id = req.param.graph
        if @PROTECT_GRAPHS and _ @PROTECTED_GRAPH_IDS .contains id
            return res.send {result:"error", message:"Graph '#{id}' is read-only."}, 403
        fs.unlink @toFile(id), @errorHandler(res, "Graph '#{id}' does not exist!")
    
    
    ### Helpers
    
    processBody: (req, res) ->
        if not req.body
            res.send {result:"error", message:"Data required!"}, 501
            return false
        
        data = req.body
        data.slug or= data.id
        data.id or= data.slug
        
        if not data.slug
            res.send {result:"error", message:"Slug required!"}, 501
            return false
        
        mkdirp @dataDir if not exists @dataDir
        return data
    
    errorHandler: (res, msg) ->
        (err) ->
            if err
                msg or= err.message or String(err)
                console.error msg
                res.send { result:"error", message:msg }, 501
            else
                res.send { result:"ok" }
    

module.exports = exports = GraphController
