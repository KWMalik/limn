_ = require 'kraken/underscore'
{ Field, FieldList, BaseView, FieldView, Scaffold
} = require 'kraken/scaffold'
{   GraphOption, GraphOptionList, TagSet,
} = require 'kraken/graph/model'



/**
 * The view for a single configurable option.
 */
GraphOptionView = exports.GraphOptionView = FieldView.extend do # {{{
    # __bind__  : <[ onClick ]>
    ctorName  : 'GraphOptionView'
    tagName   : 'div'
    className : 'field option'
    template  : require 'kraken/template/graph-option'
    
    isCollapsed : true
    
    events :
        'blur .value'                   : 'update'
        'submit .value'                 : 'update'
        'click .close'                  : 'toggleCollapsed'
        'click h3'                      : 'toggleCollapsed'
        'click .collapsed'              : 'onClick'
    
    
    update: ->
        val     = @$el.find('.value').val()
        current = @model.get 'value'
        return if val is current
        console.log "#this.update( #current -> #val )"
        @model.setValue val, {+silent}
    
    render: ->
        @__super__.render ...
        @$el.addClass 'collapsed' if @isCollapsed
        this
    
    onClick: (evt) ->
        target = $ evt.target
        # console.log "#this.onClick()", target
        @toggleCollapsed() if @$el.hasClass('collapsed') and not target.hasClass('close')
    
    toggleCollapsed: ->
        starting = @$el.hasClass 'collapsed' #@isCollapsed
        @$el.toggleClass 'collapsed'
        @isCollapsed = @$el.hasClass 'collapsed'
        # console.log "#this.toggleCollapsed!", starting, '->', @isCollapsed
        @trigger 'change:collapse', this
        this
    
# }}}



GraphOptionsScaffold = exports.GraphOptionsScaffold = Scaffold.extend do # {{{
    ctorName       : 'GraphOptionsScaffold'
    tagName        : 'form'
    className      : 'options scaffold'
    collectionType : GraphOptionList
    subviewType    : GraphOptionView
    
    
    initialize : ->
        Scaffold::initialize ...
        @render = _.debounce @render.bind(this), 50
    
    render: ->
        # console.log "#this.render() -> .isotope()"
        @__super__.render ...
        @$el.isotope do
            itemSelector    : '.field.option'
            layoutMode      : 'masonry'
            masonry         : columnWidth : 10
            # itemPositionDataEnabled : true
            # animationEngine : 'jquery'
    
    addOne: ->
        # console.log "#this.addOne!"
        view = @__super__.addOne ...
        view.on 'change:collapse render', @render
        view
    
# }}}




/**
 * Represents a Graph, including its charting options, dataset, annotations, and all
 * other settings for both its content and presentation.
 */
GraphModel = exports.GraphModel = Backbone.Model.extend do # {{{
    ctorName : 'GraphModel'
    urlRoot  : '/graphs'
    
    initialize : ->
        name = @get 'name'
        if name and not (@id or @has 'id')
            @id = @attributes.id = _.underscored name
    
    defaults : ->
        {
            name    : 'Kraken Graph'
            dataset : '/data/page_views_by_language.csv'
            options : {}
        }
    
    toString: -> "#{@ctorName}(id=#{@id}, name=#{@get 'name'}, dataset=#{@get 'dataset'})"
# }}}


GraphView = exports.GraphView = BaseView.extend do # {{{
    ctorName  : 'GraphView'
    tagName   : 'section'
    className : 'graph'
    template  : require 'kraken/template/graph'
    
    events:
        'keypress form.options .value' : 'onKeypress'
        'submit   form.options'        : 'onSubmit'
    
    
    initialize : (o={}) ->
        @model or= new GraphModel
        BaseView::initialize ...
        # console.log "#this.initialize!"
        
        @model.on 'change',  @render, this
        @model.on 'destroy', @remove, this
        
        @build()
        @viewport = @$el.find '.viewport'
        
        @scaffold = new GraphOptionsScaffold
        @$el.find 'fieldset' .append @scaffold.el
        @scaffold.collection.reset that if o.graph_spec
        
        setTimeout do
            ~> @render()
            50
    
    
    chartOptions: (values) ->
        # Handle @chartOptions k, v
        if arguments.length > 1
            [k, v] = arguments
            values = { "#k": v }
        
        options = @scaffold.collection
        if values
            for k, v in values
                options.get(k)?.setValue v
            this
        else
            options.values()
    
    render: ->
        options = @chartOptions()
        w = options.width  or= @scaffold.get 'width'  .getValue() or 480
        h = options.height or= @scaffold.get 'height' .getValue() or 320
        @viewport.css { width:w, height:h }
        
        # Remove old style, as it confuses dygraph after options update
        @viewport.attr 'style', ''
        
        console.log "#this"
        console.log do 
            "  .viewport.{ width=%s, height=%s, style=%s }"
            @viewport.css('width')
            @viewport.css('height')
            @viewport.attr 'style'
            @viewport
        console.log '  .options:', JSON.stringify options
        
        # @chart?.destroy()
        unless @chart
            @chart = new Dygraph do
                @viewport.0
                @model.get 'dataset'
                options
        else
            @chart.updateOptions options
            @chart.resize w, h
        
        this
    
    onKeypress: (evt) ->
        $(evt.target).submit() if evt.keyCode is 13
    
    onSubmit: ->
        console.log "#this.onSubmit!"
        @render()
        false
    
    toString: -> "#{@ctorName}(#{@model})"
# }}}



