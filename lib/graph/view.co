_ = require 'kraken/underscore'
{BaseView} = require 'kraken/base'
{ Field, FieldList, FieldView, Scaffold
} = require 'kraken/scaffold'
{   GraphModel, GraphOption, GraphOptionList, TagSet,
} = require 'kraken/graph/model'

DEBOUNCE_RENDER = exports.DEBOUNCE_RENDER = 100ms


/**
 * The view for a single configurable option.
 */
GraphOptionView = exports.GraphOptionView = FieldView.extend do # {{{
    # __bind__  : <[ onClick ]>
    ctorName  : 'GraphOptionView'
    tagName   : 'div'
    className : 'field option'
    template  : require 'kraken/template/graph-option'
    
    isCollapsed : true
    
    events :
        'blur .value'                        : 'update'
        'click input[type="checkbox"].value' : 'update'
        'submit .value'                      : 'update'
        'click .close'                       : 'toggleCollapsed'
        'click h3'                           : 'toggleCollapsed'
        'click .collapsed'                   : 'onClick'
    
    
    update: ->
        val     = @$el.find('.value').val()
        current = @model.get 'value'
        return if val is current
        console.log "#this.update( #current -> #val )"
        @model.setValue val, {+silent}
    
    render: ->
        @__super__.render ...
        @$el.addClass 'collapsed' if @isCollapsed
        this
    
    onClick: (evt) ->
        target = $ evt.target
        # console.log "#this.onClick()", target
        @toggleCollapsed() if @$el.hasClass('collapsed') and not target.hasClass('close')
    
    toggleCollapsed: ->
        starting = @$el.hasClass 'collapsed' #@isCollapsed
        @$el.toggleClass 'collapsed'
        @isCollapsed = not starting
        # console.log "#this.toggleCollapsed!", starting, '->', @isCollapsed
        @trigger 'change:collapse', this, @isCollapsed
        this
    
# }}}



GraphOptionsScaffold = exports.GraphOptionsScaffold = Scaffold.extend do # {{{
    ctorName       : 'GraphOptionsScaffold'
    tagName        : 'form'
    className      : 'options scaffold'
    collectionType : GraphOptionList
    subviewType    : GraphOptionView
    
    
    initialize : ->
        Scaffold::initialize ...
        @render = _.debounce @render.bind(this), DEBOUNCE_RENDER
    
    render: ->
        console.log "#this.render() -> .isotope()"
        @__super__.render ...
        @$el.isotope do
            itemSelector    : '.field.option'
            layoutMode      : 'masonry'
            masonry         : columnWidth : 10
            # itemPositionDataEnabled : true
            # animationEngine : 'jquery'
    
    addOne: (field) ->
        # console.log "#this.addOne!", field
        
        field.on 'change:value', ~>
            key   = field.get 'name'
            value = field.getValue()
            @trigger "change:#key", this, value, key, field
            @trigger "change",      this, value, key, field
        
        view = @__super__.addOne ...
        view.on 'change:collapse render', @render
        view
    
    toKVPairs: ->
        @collection.toKVPairs()
    
# }}}




GraphView = exports.GraphView = BaseView.extend do # {{{
    __bind__  : <[ render renderAll ]>
    ctorName  : 'GraphView'
    tagName   : 'section'
    className : 'graph'
    template  : require 'kraken/template/graph'
    
    events:
        'keypress form.options .value' : 'onKeypress'
        'submit   form.options'        : 'onSubmit'
    
    
    initialize : (o={}) ->
        @render    = _.debounce @render,    DEBOUNCE_RENDER
        @renderAll = _.debounce @renderAll, DEBOUNCE_RENDER
        
        @model or= new GraphModel
        BaseView::initialize ...
        # console.log "#this.initialize!"
        
        @model.on 'change:options', ~>
            changes = @model.changedAttributes()
            console.log 'Model.changed(options) ->', changes
            @chartOptions changes, {+silent}
        @model.on 'change',  @render, this
        @model.on 'destroy', @remove, this
        
        @build()
        @viewport = @$el.find '.viewport'
        
        @scaffold = new GraphOptionsScaffold
        @$el.find 'fieldset' .append @scaffold.el
        @scaffold.collection.reset that if o.graph_spec
        
        @scaffold.on 'change', (scaffold, value, key, field) ~>
            console.log "scaffold.change", value, key, field
            @model.setOption key, value, {+silent}
        
        options = @model.get 'options', {}
        @chartOptions options, {+silent}
        
        _.delay @render, DEBOUNCE_RENDER
    
    
    chartOptions: (values, opts) ->
        # Handle @chartOptions(k, v, opts)
        if arguments.length > 1 and typeof values is 'string'
            [k, v, opts] = arguments
            values = { "#k": v }
        
        options = @scaffold.collection
        if values
            for k, v in values
                options.get(k)?.setValue v, opts
            this
        else
            options.values()
    
    
    render: ->
        options = @chartOptions()
        w = options.width  or= @scaffold.get 'width'  .getValue() or 480
        h = options.height or= @scaffold.get 'height' .getValue() or 320
        @viewport.css { width:w, height:h }
        
        # Remove old style, as it confuses dygraph after options update
        @viewport.attr 'style', ''
        
        console.log "#this"
        console.log do 
            "  .viewport.{ width=%s, height=%s, style=%s }"
            @viewport.css('width')
            @viewport.css('height')
            @viewport.attr 'style'
            # @viewport
        console.log '  .options:', JSON.stringify options
        
        # @chart?.destroy()
        unless @chart
            @chart = new Dygraph do
                @viewport.0
                @model.get 'dataset'
                options
        else
            @chart.updateOptions options
            @chart.resize w, h
        
        this
    
    renderAll: ->
        _.invoke @scaffold.subviews, 'render'
        @scaffold.render()
        @render()
        this
    
    onKeypress: (evt) ->
        $(evt.target).submit() if evt.keyCode is 13
    
    onSubmit: ->
        console.log "#this.onSubmit!"
        @render()
        false
    
    
    toKVPairs: (keepDefaults=false) ->
        @scaffold.toKVPairs keepDefaults
    
    toString: -> "#{@ctorName}(#{@model})"
# }}}



