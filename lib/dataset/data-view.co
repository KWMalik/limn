Seq = require 'seq'
{ _, op,
} = require 'kraken/util'
{ BaseView, ViewList,
} = require 'kraken/base'
{ DataSetView,
} = require 'kraken/dataset/dataset-view'
{ MetricEditView,
} = require 'kraken/dataset/metric-edit-view'


/**
 * @class
 */
DataView = exports.DataView = BaseView.extend do # {{{
    __bind__       : <[ ]>
    tagName        : 'section'
    className      : 'data-ui'
    template       : require 'kraken/template/data'
    
    datasources : null
    
    
    
    constructor: function DataView
        BaseView ...
    
    initialize: ->
        @graph_id = @options.graph_id
        BaseView::initialize ...
        @metric_views = new ViewList
        @datasources = @model.sources
        @model.metrics
            .on 'add',      @addMetric,     this
            .on 'remove',   @removeMetric,  this
        @on 'ready', @onReady, this
        @load()
    
    onReady: ->
        dataset = @model
        @model.metrics.each @addMetric, this
        # @metric_edit_view = @addSubview new MetricEditView  {@graph_id, dataset, @datasources}
        # @metric_edit_view
        #     .on 'update',           @onUpdateMetric,    this
        
        @dataset_view = new DataSetView {@model, @graph_id, dataset, @datasources}
        @addSubview @dataset_view
            .on 'add-metric',       @onMetricsChanged,  this
            .on 'remove-metric',    @onMetricsChanged,  this
            .on 'edit-metric',      @editMetric,        this
        
        @render()
        this
    
    
    
    load: ->
        @wait()
        # $.getJSON '/datasources/all', (@data) ~>
        #     _.each @data, @canonicalizeDataSource, this
        #     @model.sources.reset _.map @data, -> it
        @unwait()
        # @render()
        @triggerReady()
    
    /**
     * Transform the `columns` field to ensure an Array of {label, type} objects.
     */
    canonicalizeDataSource: (ds) ->
        ds.shortName    or= ds.name
        ds.title        or= ds.name
        ds.subtitle     or= ''
        
        cols = ds.columns
        if _.isArray cols
            ds.metrics = _.map cols, (col, idx) ->
                if _.isArray col
                    [label, type] = col
                    {idx, label, type or 'int'}
                else
                    col
        else
            ds.metrics = _.map cols.labels, (label, idx) ->
                {idx, label, type:cols.types[idx] or 'int'}
        ds
    
    
    toTemplateLocals: ->
        attrs = _.clone @model.attributes
        { @graph_id, @datasources } import attrs
    
    # Don't rebuild HTML, simply notify subviews
    # render: ->
    #     @renderSubviews()
    #     @trigger 'render', this
    #     this
    
    addMetric: (metric) ->
        console.log "#this.addMetric!", metric
        return metric if @metric_views.findByModel metric
        view = new MetricEditView {model:metric, @graph_id, dataset, @datasources}
        @metric_views.push @addSubview view
        metric
    
    removeMetric: (metric) ->
        console.log "#this.removeMetric!", metric
        return unless view = @metric_views.findByModel metric
        @metric_views.remove view
        @removeSubview view
        metric
    
    editMetric: (metric) ->
        console.log "#this.editMetric!", metric
        @metric_views.invoke 'hide'
        @metric_edit_view = @metric_views.findByModel metric
        @metric_edit_view?.show()
        @onMetricsChanged()
    
    onMetricsChanged: ->
        oldMinHeight = parseInt @$el.css 'min-height'
        newMinHeight = Math.max do
            oldMinHeight
            @dataset_view.$el.height()
            @metric_edit_view?.$el.height()
        # console.log 'onMetricsChanged!', oldMinHeight, '-->', newMinHeight
        @$el.css 'min-height', newMinHeight
    
    onUpdateMetric: ->
        @renderSubviews()
# }}}
