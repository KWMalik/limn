Seq = require 'seq'
ColorBrewer = require 'colorbrewer'

{ _, op,
} = require 'kraken/util'
{ BaseModel, BaseList,
} = require 'kraken/base'
{ Metric, MetricList,
} = require 'kraken/dataset/metric-model'
{ DataSource, DataSourceList,
} = require 'kraken/dataset/datasource-model'



/**
 * @class
 */
DataSet = exports.DataSet = BaseModel.extend do # {{{
    urlRoot : '/datasets'
    
    /**
     * @type DataSourceList
     */
    sources : null
    
    /**
     * @type MetricList
     */
    metrics : null
    
    defaults : ->
        palette : null
        lines   : []
        metrics : []
    
    
    constructor: function DataSet
        BaseModel ...
    
    initialize : ->
        BaseModel::initialize ...
        @metrics = new MetricList @attributes.metrics
        @on 'change:metrics', @onMetricChange, this
    
    
    
    load: (opts={}) ->
        @resetReady() if opts.force
        return this if @loading or @ready
        
        unless @metrics.length
            return @triggerReady()
        
        console.log "#this.load()..."
        @wait()
        @loading = true
        @trigger 'load', this
        Seq @metrics.models
            .parEach_ (next, metric) ->
                metric.once 'ready', next.ok .load()
            # .parEach_ (next, metric) ->
            #     metric.on 'load-data-success', next.ok .loadData()
            .seq ~>
                console.log "#{this}.load() complete!"
                @loading = false
                @unwait() # terminates the `load` wait
                @triggerReady()
        this
    
    
    /* * * *  TimeSeriesData interface  * * * {{{ */
    
    /**
     * @returns {Array<Array>} The reified dataset, materialized to a list of rows including timestamps.
     */
    getData: ->
        _.zip ...@getColumns()
    
    /**
     * @returns {Array<Array>} List of all columns (including date column).
     */
    getColumns: ->
        [ @getDateColumn() ].concat @getDataColumns()
    
    /**
     * @returns {Array<Date>} The date column.
     */
    getDateColumn: ->
        dates = @metrics.onlyOk().invoke 'getDateColumn'
        maxLen = _.max _.pluck dates, 'length'
        _.find dates, -> it.length is maxLen
    
    /**
     * @returns {Array<Array>} List of all columns except the date column.
     */
    getDataColumns: ->
        @metrics.onlyOk().invoke 'getData'
    
    /**
     * @returns {Array<String>} List of column labels.
     */
    getLabels: ->
        [ 'Date' ].concat @metrics.onlyOk().invoke 'getLabel'
    
    # }}}
    
    getColors: ->
        @metrics.onlyOk().pluck 'color'
    
    newMetric: ->
        index = @metrics.length
        @metrics.add m = new Metric { index, color:ColorBrewer.Spectral[11][index] }
        m
    
    onMetricChange: ->
        console.log "#this.onMetricChange! ready=#{@ready}"
        @resetReady()
        @metrics.reset @get 'metrics'
        @load()
    
    # TODO: toJSON() must ensure columns in MetricList are ordered by index
    #   ...in theory, MetricList.comparator now does this
    
    
# }}}

