_  = require 'kraken/util/underscore'
op = require 'kraken/util/op'
{ BaseView,
}  = require 'kraken/base'
{ Field, FieldList,
}  = require 'kraken/scaffold/scaffold-model'


FieldView = exports.FieldView = BaseView.extend do # {{{
    tagName   : 'div'
    className : 'field'
    
    type : 'string'
    
    events :
        'blur .value'   : 'update'
        'submit .value' : 'update'
    
    
    constructor: function FieldView
        BaseView ...
    
    initialize: ->
        # console.log "#this.initialize!"
        BaseView::initialize ...
        @type = @model.get('type', 'string').toLowerCase()
    
    update: ->
        if @type is 'boolean'
            val = !! @$el.find('.value').attr('checked')
        else
            val = @model.getParser() @$el.find('.value').val()
        
        current = @model.getValue()
        return if _.isEqual val, current
        console.log "#this.update( #current -> #val )"
        @model.setValue val, {+silent}
        @trigger 'update', this
    
    render: ->
        return @remove() if @model.get 'ignore', false
        return BaseView::render ... if @template
        
        name  = @model.get 'name'
        id    = _.camelize name
        label = name
        value = @model.get 'value'
        value = '' if value!?
        
        @$el.html """
            <label class="name" for="#id">#label</label>
            <input class="value" type="text" id="#id" name="#id" value="#value">
        """
        
        this
    
# }}}


# There are several special options that, if passed, will be attached directly to the view:
#   model, collection, el, id, className, tagName, attributes

Scaffold = exports.Scaffold = BaseView.extend do # {{{
    __bind__       : <[ addOne addAll ]>
    tagName        : 'form'
    className      : 'scaffold'
    
    collectionType : FieldList
    subviewType    : FieldView
    
    
    constructor: function Scaffold
        BaseView ...
    
    initialize: ->
        CollectionType = @collectionType
        @model = @collection or= new CollectionType
        BaseView::initialize ...
        
        @collection.on 'add',   @addOne
        @collection.on 'reset', @addAll
        
        @$el.data { model:@collection, view:this } .addClass @className
    
    
    addOne: (field) ->
        # console.log "[S] #this.addOne!", @..__super__
        _.remove @subviews, field.view if field.view
        
        # avoid duplicating event propagation
        field.off 'change:value', @change, this
        
        # propagate value-change events as key-value change events
        field.on 'change:value', @change, this
        
        SubviewType = @subviewType
        view = new SubviewType model:field
        @subviews.push view
        container = if @fields then @$el.find @fields else @$el
        container.append view.render().el unless field.get 'ignore'
        view.on 'update', @change.bind(this, field)
        
        @render()
        view
    
    addAll: ->
        # _.invoke @subviews, 'remove'
        # @subviews = []
        @collection.each @addOne
        this
    
    change: (field) ->
        key   = field.get 'name'
        value = field.getValue()
        @trigger "change:#key", this, value, key, field
        @trigger "change",      this, value, key, field
        this
    
    
    toString: -> "#{@ctorName}(collection=#{@collection})"



# Proxy collection methods
<[ get at pluck invoke values toJSON toKVPairs toKV toURL ]>
    .forEach (methodname) ->
        Scaffold::[methodname] = -> @collection[methodname].apply @collection, arguments

# }}}

