_    = require 'kraken/underscore'
op   = require 'kraken/util/op'
Hash = require 'hashish'



### Scaffold Models

class exports.TagSet extends Array
    tags : {}
    
    (values=[]) ->
        @tags = {}
        @add values if values?.length
    
    has: (tag) ->
        @tags[tag]?
    
    get: (tag) ->
        return -1 unless tag
        unless @tags[tag]?
            @tags[tag] = @length
            @push tag
        @tags[tag]
    
    update: (tags) ->
        is_single = typeof tags is 'string'
        tags      = [tags] if is_single
        indices   = ( for tag of tags then @get tag )
        if is_single then indices[0] else indices
    
    toString: -> "Tags(length=#{@length}, values=[\"#{@join '", "'}\"])"


KNOWN_TAGS = exports.KNOWN_TAGS = new TagSet()

Field = exports.Field = Backbone.Model.extend do # {{{
    ctorName    : 'Field'
    idAttribute : 'name'
    
    
    initialize: ->
        @set 'value', @get('default'), {+silent} if not @has 'value'
        
        # console.log "#this.initialize!"
        @on 'all', (evt) ~> console.log "#this.trigger(#evt)"
        
        # Notify Tag indexer of category when created, to ensure all category-tags
        # get indices with colors :P
        KNOWN_TAGS.update @getCategory()
    
    # Model defaults
    defaults: ->
        {
            name     : ''
            type     : 'String'
            default  : null
            desc     : ''
            include  : 'diff'
            tags     : []
            examples : []
        }
    
    # Wrapper to ensure @set('tags') is called, as tags.push()
    # will not trigger the 'changed:tags' event.
    addTag: (tag) ->
        return this unless tag
        tags = @get('tags', [])
        tags.push tag
        @set 'tags', tags
        this
    
    # Wrapper to ensure @set('tags') is called, as tags.push()
    # will not trigger the 'changed:tags' event.
    removeTag: (tag) ->
        return this unless tag
        tags = @get('tags', [])
        _.remove tags, tag
        @set 'tags', tags
        this
    
    # Keep tag list up to date
    onTagUpdate: ->
        KNOWN_TAGS.update @get 'tags'
        this
    
    getTagIndex: (tag) ->
        KNOWN_TAGS.get tag
    
    # A field's category is its first tag.
    getCategory: ->
        @get('tags', [])[0]
    
    getCategoryIndex: ->
        @getTagIndex @getCategory()
    
    
    
    getParser: (type) ->
        # XXX: handle 'or' by returning an array of parsers?
        type = _ (type or @get 'type').toLowerCase()
        for t of <[ Integer Float Boolean Object Array Function ]>
            if type.startsWith t.toLowerCase()
                console.log "parse#t ->", @["parse#t"] unless @["parse#t"]
                return @["parse#t"]
        @parseString
    
    parseString: (v) ->
        if v? then op.toStr v else null
    
    parseInteger: (v) ->
        r = op.toInt v
        unless isNaN r then r else null
    
    parseFloat: (v) ->
        r = op.toFloat v
        unless isNaN r then r else null
    
    parseBoolean: (v) ->
        op.toBool v
    
    parseArray: (v) ->
        if v then op.toObject v else null
    
    parseObject: (v) ->
        if v then op.toObject v else null
    
    parseFunction: (fn) ->
        if fn and _.startswith String(fn), 'function'
            try eval "(#fn)" catch err
        else
            null
    
    
    getValue: (def) ->
        @getParser() @get 'value', def
    
    setValue: (v, options) ->
        def = @get 'default'
        if not v and def == null
            val = null
        else
            val = @getParser()(v)
        @set 'value', val, options
    
    clearValue: ->
        @set 'value', @get('default')
    
    isDefault: ->
        @get('value') is @get('default')
    
    
    toJSON: ->
        {id:@id} import do
            _.clone(@attributes) import { value:@getValue(), def:@get('default') }
    
    toString: -> "(#{@id}: #{@get 'value'})"
# }}}


FieldList = exports.FieldList = Backbone.Collection.extend do # {{{
    ctorName : 'FieldList'
    model : Field
    
    /**
     * Collects a map of fields to their values, excluding those set to `null` or their default.
     * @returns {Object}
     */
    values: ->
        _.synthesize do
            @models.filter -> not it.isDefault()
            -> [ it.get('name'), it.getValue() ]
    
    toString: -> "#{@ctorName}(length=#{@length})"
# }}}

