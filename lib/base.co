_  = require 'kraken/underscore'
op = require 'kraken/util/op'


/**
 * @class Base model, extending Backbone.Model, used by scaffold and others.
 * @extends Backbone.Model
 */
BaseModel = exports.BaseModel = Backbone.Model.extend do # {{{
    # A list of method-names to bind on initialize; set this on a subclass to override.
    __bind__ : []
    ctorName : 'BaseModel'
    
    initialize: ->
        @__super__ = @constructor.__super__
        _.bindAll this, ...@__bind__ if @__bind__.length
    
    serialize: (v) ->
        if v!?
            v = ''
        else if _.isBoolean v
            v =  Number v
        else if _.isObject v
            v = JSON.stringify v
        String v
    
    /**
     * Like `.toJSON()` in that it should return a plain object with no functions,
     * but for the purpose of `.toKVPairs()`, allowing you to customize the values
     * included and keys used.
     * @returns {Object}
     */
    toKVObject: ->
        kvo = _.collapseObject @toJSON()
        for k, v in kvo
            kvo[k] = @serialize v
        kvo
    
    /**
     * Serialize the model into a `www-form-encoded` string suitable for use as
     * a query string or a POST body.
     * @returns {String}
     */
    toKVPairs: (item_delim='&', kv_delim='=') ->
        _.toKVPairs @toKVObject(), item_delim, kv_delim
    
    toString: -> "#{@ctorName}(id=#{@id})"


# Class Methods
BaseModel import do
    
    /**
     * Factory method which constructs an instance of this model from a string of KV-pairs.
     * This is a class method inherited by models which extend {BaseModel}.
     * @static
     * @param {String|Object} o Serialized KV-pairs (or a plain object).
     * @returns {BaseModel} An instance of this model.
     */
    fromKVPairs: (o, item_delim='&', kv_delim='=') ->
        o   = _.fromKVPairs o, item_delim, kv_delim if typeof o is 'string'
        Cls = if typeof this is 'function' then this else this.constructor
        new Cls _.uncollapseObject o

# }}}

/**
 * @class Base collection, extending Backbone.Collection, used by scaffold and others.
 * @extends Backbone.Collection
 */
BaseList = exports.BaseList = Backbone.Collection.extend do # {{{
    ctorName : 'BaseList'
    
    toKVObject: ->
        _.collapseObject @toJSON()
    
    toKVPairs: (item_delim='&', kv_delim='=') ->
        _.toKVPairs @toKVObject(), item_delim, kv_delim
    
    toString: -> "#{@ctorName}(length=#{@length})"
# }}}


/**
 * @class Base view, extending Backbone.View, used by scaffold and others.
 * @extends Backbone.View
 */
BaseView = exports.BaseView = Backbone.View.extend do # {{{
    ctorName : 'BaseView'
    
    # List of methods to bind on initialize; set on subclass
    __bind__ : []
    
    
    
    initialize: ->
        _.bindAll this, ...@__bind__ if @__bind__.length
        @__super__ = @constructor.__super__
        
        @build()
        @model.view = this
        @$el.data { @model, view:this }
        @model.on 'change',  @render, this
        @model.on 'destroy', @remove, this
    
    toTemplateLocals: ->
        json = {value:v} = @model.toJSON()
        if _.isArray(v) or _.isObject(v)
            json.value = JSON.stringify v
        { $, _, op, @model, view:this } import json
    
    $template: (locals={}) ->
        $ @template @toTemplateLocals() import locals
    
    build: ->
        return this unless @template
        
        outer = @$template()
        @$el.html outer.html()
            .attr do
                id    : outer.attr 'id'
                class : outer.attr('class')
        
        this
    
    render: ->
        @build()
        @trigger 'render', this
        this
    
    hide   : -> @$el.hide();      this
    show   : -> @$el.show();      this
    remove : -> @$el.remove();    this
    clear  : -> @model.destroy(); @remove()
    
    
    # remove : ->
    #     if (p = @$el.parent()).length
    #         @$parent or= p
    #         # @parent_index = p.children().indexOf @$el
    #     @$el.remove()
    #     this
    # 
    # reparent : (parent=@$parent) ->
    #     parent = $ parent
    #     @$el.appendTo parent if parent?.length
    #     this
    
    toString : -> "#{@ctorName}(model=#{@model})"


# }}}


